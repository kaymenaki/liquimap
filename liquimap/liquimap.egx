pre {
	var uem = UserExperienceMap.all.first();
	var userJourneyStages : Sequence;
	for (journey in UserJourney.all) {
		for (stage in Stage.all) {
			var userJourneyStage : Sequence;
			userJourneyStage.~journey = journey;
			userJourneyStage.~stage = stage;
			userJourneyStages.add(userJourneyStage);
		}
	}
}

rule Blueprint 
	transform uem : UserExperienceMap {

	template : "blueprint.egl"

	parameters : Map {
		"path" = Sequence{uem.name, "Blueprint"},
		"format" = "html",
		"stages" = Stage.all,
		"steps" = Step.all,
		"touchpoints" = Touchpoint.all,
		"participants" = Participant.all,
		"layers" = Sequence {
			Map{
				"title" = "Participants",
				"id" = "participants"
			}
		}
	}
	
}

rule Sankey 
	transform uem : UserExperienceMap {

	template : "sankey.egl"

	parameters : Map {
		"path" = Sequence{uem.name, "Sankey"},
		"format" = "html"
	}
	
}

rule BlueprintParticipant
	transform p : Participant {
	
	template : "blueprint.egl"
	
	parameters : Map {
		"path" = Sequence{uem.name, "Blueprint", "Participants", p.name},
		"format" = "html",
		"stages" = p.getStages(),
		"steps" = p.getSteps(),
		"touchpoints" = p.getTouchpoints(),
		"participants" = Sequence{p},
		"uem" = uem
	}
	
}

rule BlueprintStage
	transform stage : Stage {
	
	template : "blueprint.egl"
	
	parameters : Map {
		"path" = Sequence{stage.eContainer().name, "Blueprint", "Stages", stage.name},
		"format" = "html",
		"stages" = Sequence{},
		"steps" = stage.steps,
		"touchpoints" = stage.steps.touchpoints.flatten(),
		"participants" = stage.getParticipants(),
		"uem" = uem
	}
	
}

rule UserFlow 
	transform uem : UserExperienceMap {

	template : "userflow.egl"

	parameters : Map {
		"path" = Sequence{uem.name, "User Flow"},
		"format" = "graphviz-dot",
		"stage" = null
	}
	
}

rule StageUserFlow 
	transform stage : Stage {

	template : "userflow.egl"

	parameters : Map {
		"path" = Sequence{uem.name, "User Flow", "Stages", stage.name},
		"format" = "graphviz-dot",
		"layers" = Sequence {
			Map {
				"id" = "adjacent",
				"title" = "Adjacent nodes",
				"active" = false
			}
		},
		"uem" = uem
	}
	
}

rule UserJourney
	transform journey : UserJourney {
	
	template : "userjourney.egl"
	
	parameters : Map {
		"path" = Sequence{journey.eContainer().name, "User Flow", "Journeys", journey.name},
		"format" = "html",
		"stage" = null,
		"uem" = uem
	}
	
}

rule UserJourneyStage
	transform userJourneyStage in: userJourneyStages {
	
	template : "userjourney.egl"
	
	parameters : Map {
		"path" = Sequence{userJourneyStage.~journey.eContainer().name, "User Flow", "Journeys", userJourneyStage.~journey.name, "Stages", userJourneyStage.~stage.name},
		"format" = "html",
		"journey" = userJourneyStage.~journey,
		"stage" = userJourneyStage.~stage,
		"uem" = uem                                                                                                                                                                  
	}
	
}

operation Participant getStages() {
	return Stage.all.select(stage|stage.steps.exists(step|self.getSteps().includes(step)));
}

operation Participant getSteps() {
	return Step.all.select(step|step.touchpoints.
		exists(touchpoint|self.getTouchpoints().includes(touchpoint)));
}

operation Participant getTouchpoints() {
	return Touchpoint.all.select(touchpoint|touchpoint.participants.includes(self));
}

operation Stage getParticipants() {
	
	return Participant.all.select(p|
		self.steps.touchpoints.flatten().participants.flatten().includes(p));
}
